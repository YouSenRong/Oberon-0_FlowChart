/* this is a oberon.cup */
/*
author: you
date: 2018/06/26
*/

/* ----------------------Preliminary Declarations Section--------------------*/
import java_cup.runtime.*;
import java.util.*;
import exceptions.*;
import callgraph.*;
import flowchart.*;
parser code
{:
	public HashMap<String, Integer> Identifier_Value_HashMap = new HashMap();
	public HashMap<String, Integer> New_type_name = new HashMap();
	public HashMap<String, Integer> Indetifier_Type_HashMap = new HashMap();
	public HashMap<String, ArrayList<Integer>> Array_HashMap = new HashMap();

	public CallGraph graph;
	
	Module main_Module;
	Procedure procedure;


:}

init with 
{:
	graph = new CallGraph();
	main_Module = new Module("Oberon-0 FlowChart");
:}

scan with {: return getScanner().next_token(); :};
	


/* ------------Declaration of Terminals and Non Terminals Section----------- */
/* Terminals (tokens returned by the scanner).  */

   /* Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */

/* reserved word */
terminal String	MODULE, PROCEDURE, CONST, TYPE, VAR, RECORD, ARRAY, OF, WHILE, DO, IF, THEN, ELSIF, ELSE, BEGIN, END, INTEGER, BOOLEAN;

/* logic operator */
terminal String	OR, AND, NOT;

/* relation operator */
terminal String	EQUAL, UNEQUAL, LESS, LESS_EQUAL, GREAT, GREAT_EQUAL;

/* addictive operator */
terminal String	PLUS, MINUS;

/* multiplicative operator */
terminal String	TIMES, DIVIDE, MOD;

/* type operator */
terminal String	COLON;

/* selected_operator */
terminal String	DOT, LBRACKET, RBRACKET;

/* assign operator */
terminal String	ASSIGN;

/* seperated operator */
terminal String	COMMA, SEMICOLON;

/* paratheses */
terminal String	LPARENTHESES, RPARENTHESES;

/* identifier */
terminal String		IDENTIFIER;

/* number */
terminal String	NUMBER;

/* Non terminals used in the grammar section.  */

   /* Non terminals that have an object value are listed first and then
   non terminals that have an integer value are listed.  An object
   value means that it can be any type, it isn't set to a specific
   type.  So it could be an Integer or a String or whatever. */

non terminal String	program, module, module_begin;
non terminal String	statement_sequence, statement, assignment, procedure_call, if_statement, elsif_statement, else_statement, while_statement;
non terminal String	declarations, const_declaration, const_list, type_declaration, type_list, var_declaration, var_list;
non terminal String	procedure_declaration, procedure_heading, procedure_body, procedure_begin;

non terminal String	formal_parameters, fp_section_list, fp_section, actual_parameters, expression_list;

non terminal String	expression, simple_expression, term, factor, expression_operator, simple_expression_operator, signed_operator, term_operator;

non terminal String	type, array_type, record_type;

non terminal String	field_lists, field_list, identifier_list;

non terminal String	selector;

non terminal String	var_epsilon;

/* -------------Precedence and Associatively of Terminals Section----------- */
precedence right	ASSIGN;
precedence left		OR, AND;
precedence right	NOT;
precedence left		EQUAL, UNEQUAL, LESS, LESS_EQUAL, GREAT, GREAT_EQUAL;
precedence left		PLUS, MINUS;
precedence left		TIMES, DIVIDE, MOD;
precedence left 	LPARENTHESES;


/* ----------------------------Grammar Section-------------------- */
/* The grammar for our parser. */


program ::= module:m
	{:
		System.out.println("Congratulation!!!");
		Procedure proc = main_Module.add("test_1");
		proc.add(new PrimitiveStatement("Yous_test_1"));
		main_Module.show();
	:};

module ::=	MODULE:m IDENTIFIER: identifier_1 SEMICOLON
			declarations
		module_begin
		END IDENTIFIER: identifier_2 DOT
		{:
		
		:};

module_begin ::=	BEGIN statement_sequence
			{:
			
			:}
			| /*epsilon*/
			{:
				RESULT = "";
			:};


declarations ::=	const_declaration: c_d
			type_declaration: c_t
			var_declaration: v_d
			procedure_declaration: p_d
			{:
				procedure.add(new PrimitiveStatement(c_d + "\n" + c_t + "\n" + v_d + "\n" + p_d +"\n"));
				//RESULT = c_d + c_t + v_d + p_d;
			:};

const_declaration ::=	CONST const_list: c_l
			{:
				RESULT = "const" + c_l;
			:} 
			| /*epsilon*/
			{:
				RESULT = "";
			:};

const_list ::=		const_list: c_l IDENTIFIER:identifier EQUAL: equal expression: e SEMICOLON: semicolon
			{:
				RESULT = c_l + identifier + equal + e + semicolon;
			:}
			| /*empty*/
			{:
				RESULT = "";
			:};

type_declaration ::=	TYPE:t type_list : t_l
			{:
				RESULT = t + t_l;
			:}
			| /*eplison*/
			{:
				RESULT = "";
			:};

type_list ::=		type_list:t_l IDENTIFIER:identifier EQUAL: equal type: t SEMICOLON: semicolon
			{:
				RESULT = t_l + identifier + equal + t + semicolon;
			:}
			| /*eplison*/
			{:
				RESULT = "";
			:};

var_declaration ::=	VAR:var var_list: v_l
			{:
				RESULT = var + v_l;
			:}
			| /*empty*/
			{:
				RESULT = "";
			:};

var_list ::=		var_list identifier_list: id_list COLON type: t SEMICOLON
			{:
				
			:}
			| /*epsilon*/
			{:
				RESULT = "";
			:};

identifier_list ::=	identifier_list: id_list COMMA: comma IDENTIFIER: identifier
			{:
				RESULT = id_list + comma + identifier;
			:}
			| IDENTIFIER: identifier
			{:
				RESULT = identifier;
			:};

procedure_declaration ::=	procedure_declaration procedure_heading SEMICOLON 
				procedure_body SEMICOLON
				| /*epsilon*/
				{:
				RESULT = "";
				:};

procedure_heading ::=	PROCEDURE IDENTIFIER: identifier formal_parameters: fp
			{:
				procedure = main_Module.add(identifier);
			:};

formal_parameters ::=	LPARENTHESES fp_section_list: fp_s_l RPARENTHESES
			{:
			:}
			| LPARENTHESES RPARENTHESES
			{:
				
			:}
			| /*epsilon*/
			{:
				RESULT = "";
			:};

fp_section_list ::=	fp_section_list: fp_s_l SEMICOLON fp_section: fp_s
			{:
			:}
			| fp_section: fp_s
			{:
			:};

fp_section ::=		var_epsilon identifier_list: id_list COLON type: t
			{:
			:};

var_epsilon ::=		VAR
			| /*eplison*/
			{:
				RESULT = "";
			:};

type ::=		IDENTIFIER: identifier
			{:
				RESULT = identifier ;	
			:}
			| INTEGER
			{:
			:}
			| BOOLEAN
			{:
			
			:}
			| array_type: t
			{:
			
			:}
			| record_type: t
			{:

			:};
array_type ::=		ARRAY expression OF type
			{:
			
			:};

record_type ::=		RECORD
				field_lists
			END;

field_lists ::=		field_lists SEMICOLON field_list
			| field_list;

field_list ::=		identifier_list COLON type
			| /*epsilon*/;

procedure_body ::=	declarations procedure_begin END IDENTIFIER: identifier
			{:
			:};

procedure_begin ::=	BEGIN statement_sequence | /*empty*/;

				
statement_sequence ::=	statement_sequence SEMICOLON statement
			| statement;

statement ::=		assignment: a
			{:
				procedure.add(new PrimitiveStatement(a));
			:}
			| procedure_call: p_c
			{:
				procedure.add(new PrimitiveStatement(p_c));
			:}
			| if_statement
			| while_statement
			| /*epsilon*/
			{:
				RESULT = "";
			:};

while_statement ::=	WHILE expression: e DO
				statement_sequence
			END
			{:
					procedure.add(new PrimitiveStatement("WHILE Statement"));
			:};

if_statement ::=	IF expression: e THEN 
				statement_sequence
			elsif_statement
			else_statement
			END
			{:
				procedure.add(new PrimitiveStatement("IF Statement"));
			:};

elsif_statement ::=	elsif_statement
			ELSIF:elsif expression:e THEN
				statement_sequence
			| /*epsilon*/
			{:
				RESULT = "";
			:};

else_statement ::=	ELSE:e statement_sequence: s_s
			| /*epsilon*/
			{:
				RESULT = "";
			:};

procedure_call ::=	IDENTIFIER: identifier actual_parameters: a_p
			{:
				RESULT = identifier + a_p;
			:};
			
actual_parameters ::=	LPARENTHESES:lp expression_list:ex_l RPARENTHESES: rp
			{:
				RESULT = lp + ex_l + rp;
			:}
			| LPARENTHESES:lp RPARENTHESES:rp
			{:
				RESULT = "(" + ")";
			:}
			| /*epsilon*/
			{:
				RESULT = "";
			:};

expression_list ::=	expression_list: ex_l COMMA: comma expression: e
			{:
				RESULT = ex_l + comma + e;	
			:}
			| expression: e
			{:
				RESULT = e;
			:};

assignment ::=		IDENTIFIER:identifier selector: s ASSIGN: assign expression: e
			{:
				RESULT = identifier + s + assign + e;
			:};

expression ::=		simple_expression: s_e_1 expression_operator: e_o simple_expression: s_e_2
			{:
				RESULT = s_e_1  + e_o  + s_e_2 ;
				/*System.out.println(RESULT);*/
			:}
			| simple_expression: s_e
			{:
				RESULT = s_e ;
				/*System.out.println(RESULT);*/
			:}
			| expression_operator simple_expression
			{:
				if (true) throw new MissingOperandException();
			:}
			| simple_expression expression_operator
			{:
				if (true) throw new MissingOperandException();
			:};

expression_operator ::=	EQUAL: equal 
			{:
				RESULT = equal ;
				/*System.out.println(RESULT);*/
			:}
			| UNEQUAL: unequal
			{:
				RESULT = unequal ;
				/*System.out.println(RESULT);*/
			:} 
			| LESS: less
			{:
				RESULT = less ;
				/*System.out.println(RESULT);*/
			:}
			| LESS_EQUAL: less_equal
			{:
				RESULT = less_equal ;
				/*System.out.println(RESULT);*/
			:}
			| GREAT: great
			{:
				RESULT = great ;
				/*System.out.println(RESULT);*/
			:}
			| GREAT_EQUAL:great_less
			{:
				RESULT = great_less ;
				/*System.out.println(RESULT);*/
			:};

simple_expression ::=	simple_expression: s_e simple_expression_operator: s_e_o term: t
			{:
				RESULT = s_e  + s_e_o  + t ;
				/*System.out.println(RESULT);*/
			:}
			| signed_operator: s_o term: t
			{:
				RESULT = s_o  + t ;
			:}
			| OR term
			{:
				if(true) throw new MissingOperandException();
			:};
			
signed_operator ::=	PLUS: plus
			{:
				RESULT = plus ;
				/*System.out.println(RESULT);*/
				
			:}
			| MINUS: minus
			{:
				RESULT = minus ;
				/*System.out.println(RESULT);*/
			:}
			| /*epsilon*/;

simple_expression_operator ::=	PLUS: plus
				{:
					RESULT = plus ;
					/*System.out.println(RESULT);*/
				
				:}	
				| MINUS: minus
				{:
					RESULT = minus ;
					/*System.out.println(RESULT);*/
				:}
				| OR: or
				{:
					/*RESULT = or ;*/
					/*System.out.println(RESULT);*/
				:};

term ::=		term: t term_operator: t_o factor: f
			{:
				RESULT = t  + t_o  + f ;
				/*System.out.println(RESULT);*/
			:}
			| factor: f
			{:
				RESULT = f ;
				/*System.out.println(RESULT);*/
			:}
			| term_operator factor
			{:
				if(true) throw new MissingOperandException(); 
			:}
			| term factor
			{:
				if(true) throw new MissingOperatorException();
			:};

term_operator ::=	TIMES: times
			{:
				RESULT = times ;
				/*System.out.println(RESULT);*/
			:}
			| DIVIDE: divides
			{:
				RESULT = divides ;
				/*System.out.println(RESULT);*/
			:}
			| MOD: mod
			{:
				RESULT = mod ;
				/*System.out.println(RESULT);*/
			:}
			| AND: and
			{:
				RESULT = and ;
				/*System.out.println(RESULT);*/
			:};

factor ::=		IDENTIFIER: identifier selector: s
			{:
				RESULT = identifier  + s ;
				/*System.out.println(RESULT);*/
			:}
			| NUMBER: number
			{:
				RESULT = number ;
				/*System.out.println(RESULT);*/
			:}
			| LPARENTHESES: lparen expression: e RPARENTHESES: rparen
			{:
				RESULT = lparen  + e  + rparen ;
				/*System.out.println(RESULT);*/
			:}
			| NOT: not factor: f
			{:
				RESULT = not  + f ;
				/*System.out.println(RESULT);*/
			:};


selector ::=		selector: s DOT: dot IDENTIFIER: identifier
			{:
				RESULT = s  + dot  + identifier ;
				/*System.out.println(RESULT);*/
			:}
			| selector:s LBRACKET: lbracket expression: e RBRACKET: rbracket
			{:
				RESULT = s + lbracket + e + rbracket ;
				/*System.out.println(RESULT);*/
			:}
			| /*epsilon*/
			{:
				RESULT = "";
				/*System.out.println(RESULT);*/
			:};

